# Maintainer: DuckSoft <realducksoft@gmail.com>
pkgname=acolors-dev-git
_pkgname=acolors
pkgver=0.1.0.r216.576f106
pkgrel=1
pkgdesc="The next GUI client for v2ray core (Development Branch)"
arch=('x86_64')
url='https://github.com/ArkToria/AColoRS'
license=('GPL3')
depends=()
makedepends=('git' 'cmake' 'clang' 'rust' 'gcc')
optdepends=('v2ray: use system v2ray core.')
provides=("${_pkgname}")
conflicts=("${_pkgname}")

source=('$pkgname::git+$url')

sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/${pkgname}/"
    printf "%s.r%s.%s" $(cat ./misc/VERSION) $(git rev-list --count HEAD) $(git rev-parse --short HEAD)
}
prepare() {
    cd "${srcdir}/${pkgname}/"

    # download dependencies
    cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
    cd "${srcdir}/${pkgname}/"

    cargo build --frozen --release
}

check() {
    cd "${srcdir}/${pkgname}/"
    cargo test \
	    --frozen --
}

package() {
    cd "${srcdir}/${pkgname}"

    # binary
    install -vDm755 -t "$pkgdir/usr/bin/" "target/release/$_pkgname"
}
